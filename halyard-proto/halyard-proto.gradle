apply plugin: 'java'
apply plugin: 'com.google.protobuf'

dependencies {
  compile 'io.grpc:grpc-all:1.8.0'
  compile 'com.google.protobuf:protobuf-java:3.0.0'
  compile 'com.google.guava:guava:23.5-jre'
  compile 'com.google.api.grpc:grpc-google-common-protos:1.0.5'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.5.1-1'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.9.0"
    }
  }

  generateProtoTasks {
    ofSourceSet('main').each { task ->
      task.builtins {
        java{
          outputSubDir = 'java'
        }
      }
      task.plugins {
        grpc {
          outputSubDir = 'java'
        }
      }
      task.descriptorSetOptions.includeImports = true
    }
  }
  generatedFilesBaseDir = "${projectDir}/build-gen/proto"
}

idea {
  module {
    sourceDirs += file("${projectDir}/build-gen/proto/main/java")
  }
}

clean {
  delete "${projectDir}/build-gen"
}
