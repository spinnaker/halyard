apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'
apply plugin: 'spinnaker.package'

ext {
  springConfigLocation = System.getProperty('spring.config.node', "${System.getProperty('user.home')}/.spinnaker/")
  repackage = System.getProperty('springBoot.repackage', "false")
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.config.node', project.springConfigLocation)
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

tasks.withType(JavaExec) {
  if (System.getProperty('DEBUG', 'false') == 'true') {
    jvmArgs '-Xdebug', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=9099'
  }
}

dependencies {
  compile spinnaker.dependency('bootActuator')
  compile spinnaker.dependency('bootWeb')
  compile spinnaker.dependency('lombok')

  compile project(':halyard-backup')
  compile project(':halyard-cli')
  compile project(':halyard-config')
  compile project(':halyard-core')
  compile project(':halyard-deploy')

  compile('org.lognet:grpc-spring-boot-starter:2.1.4')
  compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'
}

sourceSets {
  main {
    java {
      srcDir 'src/main/protoGen'
    }
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.8.0"
    }
  }

  generateProtoTasks {
    ofSourceSet('main').each { task ->
      task.builtins {
        java{
          outputSubDir = 'protoGen'
        }
      }
      task.plugins {
        grpc {
          outputSubDir = 'protoGen'
        }
      }
    }
  }
  generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen{
  doFirst{
    delete("$projectDir/src/main/protoGen")
  }
}
clean.dependsOn cleanProtoGen


tasks.bootRepackage.enabled = project.repackage

mainClassName = 'com.netflix.spinnaker.halyard.Main'

def cliScript = project.tasks.create('createCliStartScripts', CreateStartScripts) {
  dependsOn(project.tasks.startScripts)
  mainClassName = 'com.netflix.spinnaker.halyard.cli.Main'
  applicationName = 'hal'
  outputDir = project.tasks.startScripts.outputDir
  classpath = project.tasks.startScripts.classpath
}

tasks.installDist.dependsOn(cliScript)
tasks.distZip.dependsOn(cliScript)
tasks.distTar.dependsOn(cliScript)
